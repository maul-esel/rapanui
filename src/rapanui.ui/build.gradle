plugins {
  id "java"
  id "eclipse"
  id "application"
  id "com.github.johnrengelman.shadow" version '1.2.3'
}

group 'rapanui'
version '1.0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  compile project(':rapanui.dsl')
  compile project(':rapanui.core')
}

mainClassName = 'rapanui.ui.Program'

sourceSets {
  main {
    java {
      srcDir 'src'
    }
    resources {
      srcDir 'resources'
      srcDir '../rapanui.library'
      includes = ['*.raps', '*.ttf', 'DejaVuSans_Font_LICENSE']
    }
  }
}

eclipse.project {
  natures 'org.eclipse.buildship.core.gradleprojectnature'
  buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
}

run {
  if (project.hasProperty('args'))
    args project.getProperty('args').split(' ')
}

shadowJar {
  ['model/*', 'schema/*', 'NOTICE', '.api_description', 'LICENSE', 'about.html', 'about.ini',
   'about.mappings', 'about.properties', 'about_org.eclipse.xtend.lib.html',
   'about_org.eclipse.xtend.lib.macro.html', 'about_org.eclipse.xtext.xbase.lib.html',
   'modeling32.png', 'plugin.properties', 'plugin.xml', 'log4j.properties', 'META-INF/DEPENDENCIES',
   'META-INF/LICENSE', 'META-INF/NOTICE', 'META-INF/MANIFEST.MF_gen', 'META-INF/maven/**'].each {
    f -> exclude f
  }
}