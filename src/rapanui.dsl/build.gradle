plugins {
  id "org.xtext.builder" version "1.0.4" // used to build Xcore model
  id "java"
  id "eclipse"
}

group 'rapanui'
version '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar.manifest {
  from 'META-INF/MANIFEST.MF'
}

repositories {
  mavenCentral()
  jcenter()
}

/* Configuration for Xcore */
xtext {
  version = '2.9+'
  languages {
    xcore {
      setup = 'org.eclipse.emf.ecore.xcore.XcoreStandaloneSetup'
      generator.outlet.producesJava = true
    }
  }
  sourceSets {
    main {
      srcDir 'src'
      output {
        dir(xtext.languages.xcore.generator.outlet, 'xcore-gen')
      }
    }
  }
}

/* Java source code. 'xcore-gen' is added by the Xcore config above. */
sourceSets {
  main {
    java {
      srcDir 'src'
      srcDir 'src-gen'
      srcDir 'xcore-gen'
    }

    resources {
      // include generated *.xtextbin file in JAR
      srcDir 'src-gen'
    }
  }
}

configurations {
  xtextWorkflow // for workflow execution
}

dependencies {
  // used in this project
  compile 'org.eclipse.xtext:org.eclipse.xtext:2.9.+'

  // DSL generation
  xtextWorkflow files('src')
  xtextWorkflow 'org.eclipse.emf:org.eclipse.emf.codegen.ecore.xtext:1.2.+'
  xtextWorkflow 'org.eclipse.emf:org.eclipse.emf.ecore.xcore:1.3.+'
  xtextWorkflow 'org.eclipse.emf:org.eclipse.emf.ecore.xcore.lib:1.1.+'
  xtextWorkflow 'org.eclipse.xtext:org.eclipse.xtext.xbase:2.9.+'
  xtextWorkflow 'org.eclipse.xtext:org.eclipse.xtext.xtext:2.9.+'
  xtextWorkflow 'org.ow2.asm:asm:5.1'

  // Xcore dependencies
  xtextLanguages 'org.eclipse.emf:org.eclipse.emf.ecore.xcore:1.3.+'
  xtextLanguages 'org.eclipse.emf:org.eclipse.emf.ecore.xcore.lib:1.1.+'
  xtextLanguages 'org.eclipse.emf:org.eclipse.emf.codegen.ecore:2.11.+'
  xtextLanguages 'org.eclipse.emf:org.eclipse.emf.codegen.ecore.xtext:1.2.+'
  xtextLanguages 'org.eclipse.jdt:org.eclipse.jdt.core:3.10.+'
  xtextLanguages 'org.eclipse.xtext:org.eclipse.xtext.ecore:2.9.+'

  // also Xcore dependency, which must apparently be in this configuration
  compileOnly 'org.eclipse.emf:org.eclipse.emf.ecore.xcore.lib:1.1.+'
}

/* delete generated code */
clean {
  delete 'xcore-gen'
  delete 'src-gen'
}

task createGenDirs << {
  mkdir 'xcore-gen'
  mkdir 'src-gen'
}
tasks.eclipse.dependsOn createGenDirs

/* language artifact generation by workflow execution */
task(generateLang, type: JavaExec) {
  inputs.files "src/rapanui/dsl/GenerateDsl.mwe2", "src/rapanui/dsl/Dsl.xtext"
  outputs.dir "src-gen"

  dependsOn generateXtext // xcore model needs to be generated first
  dependsOn tasks.eclipse // apparently the eclipse files are needed

  classpath configurations.xtextWorkflow
  main = "org.eclipse.emf.mwe2.launch.runtime.Mwe2Launcher"
  args "src/rapanui/dsl/GenerateDsl.mwe2"
}

/* Workflow must be run before java compilation. */
compileJava.dependsOn generateLang

eclipse {
  project {
    natures 'org.eclipse.buildship.core.gradleprojectnature'
    natures 'org.eclipse.pde.PluginNature'
    natures 'org.eclipse.xtext.ui.shared.xtextNature'

    buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
    buildCommand 'org.eclipse.xtext.ui.shared.xtextBuilder'
    buildCommand 'org.eclipse.pde.SchemaBuilder'
    buildCommand 'org.eclipse.pde.ManifestBuilder'
  }

  classpath {
    containers 'org.eclipse.pde.core.requiredPlugins'
  }
}